# https://gist.github.com/amir-saniyan/de99cee82fa9d8d615bb69f3f53b6004
function(embed_resource resource_file_name source_file_name variable_name)
  file(READ ${resource_file_name} hex_content HEX)
  string(REPEAT "[0-9a-f]" 32 column_pattern)
  string(REGEX REPLACE "(${column_pattern})" "\\1\n" content "${hex_content}")
  string(REGEX REPLACE "([0-9a-f][0-9a-f])" "0x\\1, " content "${content}")
  string(REGEX REPLACE ", $" "" content "${content}")
  set(array_definition "static const char ${variable_name}[] =\n{\n${content}\n};")
  set(source "// Auto generated file.\n${array_definition}\n")
  file(APPEND "${source_file_name}" "${source}")
endfunction()
set(generated_bas "../include/generated/cst_run_bas.h")
file(WRITE "${generated_bas}" "")
embed_resource("../tool/run.bas" "${generated_bas}" "SRC_CST_RUN_BAS")

# https://stackoverflow.com/a/27206982
set(generated_inc "../include/generated/inc_chunk_cu.h")
file(WRITE "${generated_inc}" "")
embed_resource("../src/chunk.cu" "${generated_inc}" "SRC_CHUNK_CU")
file(GLOB inc_list "../include/*.h" "../include/utils/*.h")
set(inc_file_list "")
foreach(inc ${inc_list})
  string(REGEX MATCH "include/(.+)$" filename ${inc})
  string(REGEX REPLACE "\\.| |-" "_" shortname "${filename}")
  string(REPLACE "/" "_" shortname "${shortname}")
  string(TOUPPER ${shortname} uppername)
  set(inc_file_list "${inc_file_list}\"${filename}\",INC_${uppername},\n")
  embed_resource(${inc} "${generated_inc}" "INC_${uppername}")
endforeach()
set(inc_file_list "static const char* INC_FILES[] = {\n${inc_file_list}\"\"\n};")
file(APPEND "${generated_inc}" "${inc_file_list}")
